import java.text.SimpleDateFormat
import groovy.json.JsonSlurper

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.mini2Dx:gettext-gradle-plugin:1.10.4'
    }
}

plugins {
    id 'java'
    id 'application'

    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.adarshr.test-logger' version '3.2.0'
    id 'edu.sc.seis.macAppBundle' version '2.3.0'
    id 'edu.sc.seis.launch4j' version '2.5.3'
    id 'de.undercouch.download' version '5.1.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.github.ben-manes.versions' version '0.42.0'
}

apply plugin: 'org.mini2Dx.gettext'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'com.atlauncher'
version = rootProject.file('src/main/resources/version').text.trim().replace('.Beta', '')

repositories {
    mavenCentral()
    gradlePluginPortal()
    maven {
        url 'https://libraries.minecraft.net'
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    implementation "net.java.dev.jna:jna:${findProperty("jna.version")}"
    implementation "net.java.dev.jna:jna-platform:${findProperty("jna.version")}"
    implementation "com.google.code.gson:gson:${findProperty("gson.version")}"
    implementation "com.google.guava:guava:${findProperty("guava.version")}"
    implementation "org.tukaani:xz:${findProperty("xz.version")}"
    implementation "com.mojang:authlib:${findProperty("authlib.version")}"
    implementation "net.iharder:base64:${findProperty("base64.version")}"
    implementation "com.github.Vatuu:discord-rpc:${findProperty("discord-rpc.version")}"
    implementation "net.sf.jopt-simple:jopt-simple:${findProperty("jopt-simple.version")}"
    implementation "org.zeroturnaround:zt-zip:${findProperty("zt-zip.version")}"
    implementation "com.squareup.okhttp3:okhttp:${findProperty("okhttp.version")}"
    implementation "com.squareup.okhttp3:okhttp-tls:${findProperty("okhttp.version")}"
    implementation "net.mikehardy:google-analytics-java:${findProperty("google-analytics.version")}"
    implementation "io.sentry:sentry:${findProperty("sentry.version")}"
    implementation "org.mini2Dx:gettext-lib:${findProperty("get-text.version")}"
    implementation "org.apache.logging.log4j:log4j-api:${findProperty("log4j.version")}"
    implementation "org.apache.logging.log4j:log4j-core:${findProperty("log4j.version")}"
    implementation "com.sangupta:murmur:${findProperty("murmur.version")}"
    implementation "org.apache.commons:commons-lang3:${findProperty("commons-lang.version")}"
    implementation "org.apache.commons:commons-text:${findProperty("commons-text.version")}"
    implementation "com.formdev:flatlaf:${findProperty("flat-laf.version")}"
    implementation "com.formdev:flatlaf-extras:${findProperty("flat-laf.version")}"
    implementation "com.github.oshi:oshi-core:${findProperty("oshi-core.version")}"
    implementation "net.freeutils:jlhttp:${findProperty("jlhttp.version")}"
    implementation "joda-time:joda-time:${findProperty("joda-time.version")}"
    implementation "org.apache.commons:commons-compress:${findProperty("commons-compress.version")}"
    implementation "org.commonmark:commonmark:${findProperty("commonmark.version")}"
    implementation "com.github.hypfvieh:dbus-java:${findProperty("dbus-java.version")}"

    testImplementation "junit:junit:${findProperty("junit.version")}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${findProperty("junit-jupiter.version")}"
    testImplementation "org.assertj:assertj-swing-junit:${findProperty("assertj-swing-junit.version")}"
    testImplementation "org.mock-server:mockserver-netty:${findProperty("mockserver-netty.version")}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${findProperty("junit-jupiter.version")}"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${findProperty("junit-jupiter.version")}"
}

application {
    mainClass = 'com.atlauncher.App'
    applicationDefaultJvmArgs = [
        "-Djna.nosys=true",
        "-Djava.net.preferIPv4Stack=true",
        "-Dawt.useSystemAAFontSettings=on",
        "-Dswing.aatext=true"
    ]
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()

    testlogger {
        theme 'mocha'
    }
}

jar {
    manifest {
        attributes(
                'SplashScreen-Image': '/assets/image/splash-screen.png',
                'Implementation-Title': project.name,
                'Implementation-Version': archiveVersion,
                'Implementation-Vender': 'ATLauncher',
                'Main-Class': 'com.atlauncher.App',
                'Multi-Release': 'true'
        )
    }

}

gettext {
    translations {
        srcDir = 'src'
        include = 'main/java/com/atlauncher/**/*.java'
        excludes = [
            'main/java/com/atlauncher/adapter/**/*.java',
            'main/java/com/atlauncher/annot/**/*.java',
            'main/java/com/atlauncher/collection/**/*.java',
            'main/java/com/atlauncher/evnt/**/*.java',
            'main/java/com/atlauncher/exceptions/**/*.java',
            'main/java/com/atlauncher/interfaces/**/*.java',
            'main/java/com/atlauncher/listener/**/*.java',
            'main/java/com/atlauncher/utils/**/*.java'
        ]
        commentFormat = ' #. '
        outputFilename = 'translations.pot'
    }
}

license {
    header = project.file('LICENSEHEADER')
    include '**/*.java'
    exclude 'io/github/**/*.java'
    exclude 'net/minecraft/**/*.java'
    exclude 'com/atlauncher/gui/layouts/WrapLayout.java'
    newLine = false
    properties {
        year = currentYear()
    }
}

shadowJar {
    classifier = null
    minimize {
        exclude(dependency('org.apache.logging.log4j:.*:.*'))
        exclude(dependency('com.formdev:.*:.*'))
        exclude(dependency('com.github.jnr:.*:.*'))
        exclude(dependency('com.github.hypfvieh:.*:.*'))
        exclude(dependency('org.apache.commons:commons-compress:.*'))
    }

    // these are included by dbus-java which is only used on Linux
    exclude("jni/x86_64-Windows/")
    exclude("jni/x86_64-SunOS/")
    exclude("jni/x86_64-OpenBSD/")
    exclude("jni/x86_64-FreeBSD/")
    exclude("jni/x86_64-DragonFlyBSD/")
    exclude("jni/sparcv9-SunOS/")
    exclude("jni/ppc-AIX/")
    exclude("jni/ppc64-AIX/")
    exclude("jni/i386-Windows/")
    exclude("jni/i386-SunOS/")
    exclude("jni/Darwin/")

    classifier = ''
}

macAppBundle {
    mainClassName = 'com.atlauncher.App'
    appName = 'ATLauncher'
    appStyle = 'universalJavaApplicationStub'
    runtimeConfigurationName = 'shadow'
    jarTask = 'shadowJar'
    icon = 'src/main/resources/assets/image/icon.icns'
    javaProperties.put('user.dir', '$APP_ROOT/Contents/Java')
    javaProperties.put('apple.laf.useScreenMenuBar', 'true')
    javaExtras.put("-Djna.nosys", "true")
    javaExtras.put("-Djava.net.preferIPv4Stack", "true")
    javaExtras.put("-Dawt.useSystemAAFontSettings", "on")
    javaExtras.put("-Dswing.aatext", "true")
    bundleExtras.put("JVMVersion", project.targetCompatibility.toString() + "+")
}

copyToResourcesJava {
    rename("ATLauncher-${project.version}.jar", "ATLauncher.jar")
}

def currentYear() {
    def df = new SimpleDateFormat("yyyy")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

launch4j {
    outfile = "ATLauncher-${project.version}.exe"
    jreMinVersion = "${project.targetCompatibility.toString()}"
    mainClassName = 'com.atlauncher.App'
    icon = "${projectDir}/src/main/resources/assets/image/icon.ico"
    version = "${project.version}"
    textVersion = "${project.version}"
    copyright = "2013-${currentYear()} ${project.name}"
    companyName = "${project.name}"
    bundledJrePath = "jre/;%JAVA_HOME%;%PATH%"
    jvmOptions = [
        "-Djna.nosys=true",
        "-Djava.net.preferIPv4Stack=true",
        "-Dawt.useSystemAAFontSettings=on",
        "-Dswing.aatext=true"
    ]
}

artifacts {
    archives shadowJar
    archives file(project.tasks.jar.getArchivePath().getPath().replace('.jar', '.exe').replace('libs', 'launch4j'))
    archives file(project.tasks.jar.getArchivePath().getPath().replace('.jar', '.zip').replace('libs', 'distributions'))
}

test {
    if (JavaVersion.current().isJava9Compatible()) {
        jvmArgs '--add-opens=java.base/sun.security.x509=ALL-UNNAMED'
    }
}

task copyArtifacts(type: Copy) {
    dependsOn build
    from shadowJar
    from file(project.tasks.jar.getArchivePath().getPath().replace('.jar', '.exe').replace('libs', 'launch4j'))
    from file(project.tasks.jar.getArchivePath().getPath().replace('.jar', '.zip').replace('libs', 'distributions'))
    into "${projectDir}/dist"
}

task downloadNewerUniversalJavaApplicationStub(type: Download) {
    description 'Downloads newer universalJavaApplicationStub'
    src 'https://raw.githubusercontent.com/tofi86/universalJavaApplicationStub/2dbbf92b35e61194266c985c8bc6b411053a1b4a/src/universalJavaApplicationStub'
    dest file("$buildDir/macApp/${project.name}.app/Contents/MacOS/universalJavaApplicationStub")
    overwrite true
}

task createTestLauncherDir {
    project.file('testLauncher/dev').mkdirs()
}

task createMacApp(type: Zip) {
    dependsOn createApp, shadowJar
    from("$buildDir/macApp") {
        include "${project.name}.app/**"
        exclude "${project.name}.app/Contents/MacOS"
    }
    from("$buildDir/macApp") {
        include "${project.name}.app/Contents/MacOS/**"
        fileMode 0777
    }
    archiveName = "${project.name}-${project.version}.zip"
}

copyArtifacts.finalizedBy {
    println 'ATLauncher has been built. Distribution files are located in the dist directory.'
}

clean.doFirst {
    delete "${projectDir}/dist"
}

project.afterEvaluate {
    tasks.check {
        dependsOn -= tasks.find {
            it.name.equals("checkLicenses")
        }
    }
}

def shouldIgnoreUpdate = { String version -> return ['ALPHA', 'BETA', 'RC', '-M'].any { it -> version.toUpperCase().contains(it) }}
tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        shouldIgnoreUpdate(it.candidate.version)
    }
}

build.finalizedBy copyArtifacts
shadowJar.dependsOn jar
build.dependsOn createExe, createMacApp
createApp.finalizedBy downloadNewerUniversalJavaApplicationStub
