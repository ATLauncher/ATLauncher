plugins {
    id 'java'

    id 'idea'
    id 'eclipse'

    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'edu.sc.seis.macAppBundle' version '2.1.5'
}

group = 'com.atlauncher'
archivesBaseName = project.name.toLowerCase()
version = '3.2.3.2'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories {
    mavenCentral()
    maven {
        url 'https://libraries.minecraft.net'
    }
}

dependencies {
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
    compile 'org.tukaani:xz:1.5'
    compile 'com.mojang:authlib:1.5.17'
    compile 'org.apache.logging.log4j:log4j-api:2.0-beta9'
    compile 'org.apache.logging.log4j:log4j-core:2.0-beta9'
    compile 'com.google.guava:guava:17.0'

    testCompile 'junit:junit:4.11'
}

jar {
    manifest {
        attributes(
                'SplashScreen-Image': '/assets/image/SplashScreen.png',
                'Implementation-Title': project.name,
                'Implementation-Version': version,
                'Implementation-Vender': 'RyanTheAllmighty',
                'Main-Class': 'com.atlauncher.App'
        )
    }
}

shadowJar {
    classifier = ''
}

macAppBundle {
    mainClassName = 'com.atlauncher.App'
    icon = 'src/main/resources/assets/image/Icon.icns'
}

artifacts {
    archives shadowJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}
